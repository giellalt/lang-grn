
! n(d)a- VERB  -(r)i
! 
! ndaguatai
! ndereguatai
! ndoguatai
! nda:
! delete a in front of vowel-initial prefix
! delete d in front of nasal stems

! another prefix
! t(a)- - subjunctive?
! copies following vowel if prefixed to a consonant, except for with pe.
! tapuka, terepuka, topuka, tajapuka, toropuka, tapepuka, topuka

! NEG+Verb+Mood+NEG ASPECT
! nd-aha-se-i
! NEG-GO-Des-NEG
! 
! n-asẽ-i va'ekue
! NEG-LEAVE-NEG Remote

! ----------------- !
! Prefix morphology !
! ----------------- !

LEXICON Verb
@P.NEG.ON@:nda@P.NEG.ON@ conjugationclasses ;
                         conjugationclasses ;


LEXICON conjugationclasses
V+: arealm ;
V+: airealm ;
V+: chendalm ;
V+: guearealm ;



LEXICON arealm ! gueareal conjugation class
       arealobjs ;
Inf+:  areal ;
        arealdecl ;

LEXICON arealobjs ! areal verbs with objects instead of subjects (only one possible)
1SgObj+:che%> acaus ;
1SgSub+2pSgObj+:ro%> acaus ;
2SgObj+:nde%^NAS%> acaus ; ! *ndemboguata is possible, not nemboguata

LEXICON guearealm ! gueareal conjugation class
Inf+:gue%>  areal ;
            arealdecl ;


LEXICON arealdecl
1Sg+:a%>  acaus ;
2Sg+:re%>  acaus ;
3Sg+:o%>  acaus ;
1Pli+:ja%^NAS%>  acaus ;
1Plx+:ro%>  acaus ;
2Pl+:pe%>  acaus ;
3Pl+:o%>  acaus ;

! caus can be applied to stative verbs?
LEXICON acaus
Caus+:mbo%^NAS%>              areal ;
                              areal ;


LEXICON airealm ! aireal  conjugation class
Inf+: aireal ;
1Sg+:ai%>  aireal ;
2Sg+:rei%>  aireal ;
3Sg+:oi%>  aireal ;
1Pli+:jai%^NAS%>  aireal ;
1Plx+:roi%>  aireal ;
2Pl+:pei%>  aireal ;
3Pl+:oi%>  aireal ;


LEXICON chendalm ! chendal  conjugation class
Inf+:  chendalcaus ;
1Sg+:che%>  chendalcaus ;
2Sg+:nde%^NAS%>  chendalcaus ;
3Sg+:i%>  chendalcaus ;
1Pli+:ñande%^NAS%>  chendalcaus ;
1Plx+:ore%>  chendalcaus ;
2Pl+:pende%^NAS%>  chendalcaus ;
3Pl+:i%>  chendalcaus ;

LEXICON chendalcaus
Caus+:mbo%^NAS%>            chendal ;  ! chemokane'õ doesn't appear; nasal harmony
                            chendal ;



!!            walk        use          be.big
!! 1s       a-guata      ai-poru      che-tuicha
!! 2s       re-guata      rei-poru      nde-tuicha
!! 3s       o-guata      oi-poru       i-tuicha
!! 1pi      ja-guata      jai-poru      ñande-tuicha
!! 1px      ro-guata      roi-poru      ore-tuicha
!! 2p       pe-guata      pei-poru      pende-tuicha
!! 3p       o-guata      oi-poru      i-tuicha



! ----------------- !
! Suffix morphology !
! ----------------- !

LEXICON caus2
               ASPECT ; ! No mood suffix
+Caus2:%>uka   moodloop ;

LEXICON moodloop
caus2 ;

LEXICON ASPECT
               SE ;
+Irr:%>ta      moodloop ;
+Term:%>ramo   moodloop ;
+Cond:%>rõ     moodloop ;
+Compl:%>pa    moodloop ;
+Dim:%>mi      moodloop ;

! following can co-occur: oguerovéma 
! -ve 'still, more'
! -ma 'yet, already'
! -iti 'still, yet'




LEXICON SE
+Des:%>se      NEG ;
               NEG ;

LEXICON NEG
@D.NEG@     K ;
@R.NEG.ON@     NEGSUF ;


! Delete r if non-i final vowel

LEXICON NEGSUF
+Neg:%>ri K ;

